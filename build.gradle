buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://dl.bintray.com/astrarre/maven' }
    }

    dependencies {
        classpath 'io.github.astrarre:astrarre-stripper:1.1.4'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id "fabric-loom" version "0.6-SNAPSHOT"
}


version '0.0.0'

loom {
    shareCaches = true
    //runDir = "run"
}

apply from: 'scripts/allprojects.gradle'

dependencies {
    dependsOn(project, subprojects)
}

sourceSets {
    abstracter {
        runtimeClasspath += configurations.minecraftNamed
        compileClasspath += configurations.minecraftNamed
    }
}

static def fapi(Project proj) {
    proj.configure(proj) {
        dependencies {
            modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
        }
    }
}

static def nanoevents(Project proj) {
    proj.configure(proj) {
        def nano_events_ver = "3.0.6"
        def nano_events_ap = "1.0.4"
        modImplementation("net.devtech:NanoEvents:$nano_events_ver") {
            transitive = false
        }

        testmodCompileOnly "net.devtech:nanoevents-ap:$nano_events_ap"
        compileOnly "net.devtech:nanoevents-ap:$nano_events_ap"
        annotationProcessor "net.devtech:nanoevents-ap:$nano_events_ap"
        testmodAnnotationProcessor "net.devtech:nanoevents-ap:$nano_events_ap"

        processResources {
            from(sourceSets.main.output.classesDirs.singleFile) {
                include "nanoevents/**"
            }
        }
    }
}

static def dependsOn(Project proj, Iterable<Project> projs) {
    proj.dependencies {
        projs.each {
            compile it
            testmodCompile it.sourceSets.main.output
        }
    }
    proj.publishing {
        publications {
            mavenJava(MavenPublication) {
                pom.withXml {
                    def depsNode = asNode().appendNode("dependencies")
                    projs.each {
                        def depNode = depsNode.appendNode("dependency")
                        depNode.appendNode("groupId", it.group)
                        depNode.appendNode("artifactId", "$it.name-fabric")
                        depNode.appendNode("version", it.version)
                        depNode.appendNode("scope", "compile")
                    }
                }
            }
            mavenApi(MavenPublication) {
                pom.withXml {
                    def depsNode = asNode().appendNode("dependencies")
                    projs.each {
                        def depNode = depsNode.appendNode("dependency")
                        depNode.appendNode("groupId", it.group)
                        depNode.appendNode("artifactId", "$it.name-api")
                        depNode.appendNode("version", it.version)
                        depNode.appendNode("scope", "compile")
                    }
                }
            }
        }
    }
}

static def dependsOn(Project proj, String... projects) {
    List<Project> projs = projects.collect { proj.rootProject.project(it) }
    dependsOn(proj, projs)
}

fapi(project)

apply from: 'scripts/abstraction.gradle'